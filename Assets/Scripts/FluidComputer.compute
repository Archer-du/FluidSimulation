#pragma kernel ResetCounter
#pragma kernel InsertToBucket
#pragma kernel DebugHash
#pragma kernel PrefixSum1
#pragma kernel PrefixSum2
#pragma kernel PrefixSum3
#pragma kernel Sort
#pragma kernel CalcHashRange
#pragma kernel CalcDensity
#pragma kernel CalcForces
#pragma kernel Step

#define PI 3.1415926535
#define THREAD_NUM 1024

#define GRIDS_PER_DIAMETER    2

float3 gridScale;
float3 gridOffset;
uint numHash;

// w分量是密度
struct Particle {
    float4 pos;
    float4 vel;
};

RWStructuredBuffer<uint> hashes;
RWStructuredBuffer<uint> localIndices;
RWStructuredBuffer<uint> inverseIndices;

// 粒子数量
uint numParticles;

// 相邻粒子判定半径
float radius;
float radiusSqr;

float gasConst;
float restDensity;
float mass;
float viscosity;
float gravity;
float deltaTime;

float4 planes[7];

float poly6Coeff;
float spikyCoeff;
float viscoCoeff;

uint moveBeginIndex;
uint moveSize;
float3 movePos;
float3 moveVel;

RWStructuredBuffer<Particle> particles;
RWStructuredBuffer<Particle> sorted;

RWStructuredBuffer<float4> forces;

RWStructuredBuffer<float4> mean;
RWStructuredBuffer<float3> cov;

RWStructuredBuffer<uint2> hashRange;

#define PREFIX_SUM_ARRAY_NAME globalHashCounter

#include "PrefixSum.cginc"

RWStructuredBuffer<uint> hashDebug;
RWStructuredBuffer<int3> hashValueDebug;

uint calculateHash(int3 normed) {
    uint h = (normed.z & 255) | ((normed.y & 15) << 8) | ((normed.x & 255) << 12);
    return h;
}

float poly6(float d) {
    return poly6Coeff * pow(radiusSqr - d, 3);
}

float spiky(float l) {
    return spikyCoeff * pow(radius - l, 2);
}

float visco(float l) {
    return viscoCoeff * (radius - l);
}

float isotropic(float d) {
    return 1 - pow(d / radiusSqr, 3);
}

void checkPlane(float4 plane, inout float3 pos, inout float3 vel) {
    const float eps = 0.001;
    float3 planePoint;
    if (plane.x != 0)       planePoint = float3(-plane.w / plane.x, 0, 0);
    else if (plane.y != 0)  planePoint = float3(0, -plane.w / plane.y, 0);
    else                    planePoint = float3(0, 0, -plane.w / plane.z);

    float3 planeNormal = plane.xyz;

    float3 pointToParticle = pos - planePoint;

    float d = dot(planeNormal, pointToParticle);

    if (d < 0) {
        pos -= (d - eps) * planeNormal;

        if (dot(vel, planeNormal) < 0) {
            vel -= dot(vel, planeNormal) * planeNormal;
        }
    }
}

#define BEGIN_FOREACH_PARTICLES                                         \
int3 base = floor(GRIDS_PER_DIAMETER/2.0 * (pi.pos.xyz / radius - 1));  \
for (uint3 dxyz = 0; dxyz.x < (GRIDS_PER_DIAMETER+1)*(GRIDS_PER_DIAMETER+1)*(GRIDS_PER_DIAMETER+1); dxyz += uint3(1, (GRIDS_PER_DIAMETER+1), (GRIDS_PER_DIAMETER+1)*(GRIDS_PER_DIAMETER+1))) {   \
    uint h = calculateHash(base + int3(dxyz / ((GRIDS_PER_DIAMETER+1)*(GRIDS_PER_DIAMETER+1)) % (GRIDS_PER_DIAMETER+1)));            \
    uint2 range = hashRange[h];                                 \
    for (; range.x < range.y; range.x++) {                      \
        Particle pj = sorted[range.x];                                \
        float3 diff = pi.pos.xyz - pj.pos.xyz;                  \
        float d = dot(diff, diff);                              \

#define END_FOREACH_PARTICLES   }}


[numthreads(THREAD_NUM,1,1)]
void ResetCounter (uint3 id : SV_DispatchThreadID)
{
    if (id.x < numHash) {
        globalHashCounter[id.x] = 0;
    }
}


[numthreads(THREAD_NUM,1,1)]
void DebugHash (uint3 id : SV_DispatchThreadID)
{
    if (id.x < numHash) {
        if (globalHashCounter[id.x] > 0) {
            InterlockedAdd(hashDebug[0], 1);
            InterlockedMax(hashDebug[1], globalHashCounter[id.x]);
        }
    }
    if (id.x < numParticles) {
        Particle pi = sorted[id.x];

        uint totalAccessCount = 0;
        uint neighborCount = 0;
        BEGIN_FOREACH_PARTICLES
            totalAccessCount++;
            if (d < radiusSqr) neighborCount++;
        END_FOREACH_PARTICLES

        InterlockedAdd(hashDebug[2], totalAccessCount);
        InterlockedAdd(hashDebug[3], neighborCount);

        int3 normed = floor(GRIDS_PER_DIAMETER/2.0 * (particles[id.x].pos.xyz / radius));
        hashValueDebug[id.x] = normed;
    }
}

[numthreads(THREAD_NUM,1,1)]
void CalcHashRange (uint3 id : SV_DispatchThreadID)
{
    if (id.x < numHash) {
        uint begin = id.x ? globalHashCounter[id.x-1] : 0;
        uint end = globalHashCounter[id.x];
        hashRange[id.x] = uint2(begin, end);
    }
}


[numthreads(THREAD_NUM,1,1)]
void InsertToBucket (uint3 id : SV_DispatchThreadID)
{
    if (id.x < numParticles) {
        int3 normed = floor(GRIDS_PER_DIAMETER/2.0 * (particles[id.x].pos.xyz / radius));
        uint h = calculateHash(normed);
        hashes[id.x] = h;

        uint localIndex;
        InterlockedAdd(globalHashCounter[h], 1, localIndex);

        localIndices[id.x] = localIndex;
    }
}

[numthreads(THREAD_NUM,1,1)]
void Sort (uint3 id : SV_DispatchThreadID)
{
    if (id.x < numParticles) {
        uint sortedIndex = 0;
        uint h = hashes[id.x];
        if (h > 0) {
            sortedIndex += globalHashCounter[h-1];
        }
        sortedIndex += localIndices[id.x];

        sorted[sortedIndex] = particles[id.x];

        inverseIndices[sortedIndex] = id.x;
    }
}




[numthreads(THREAD_NUM,1,1)]
void CalcDensity (uint3 id : SV_DispatchThreadID)
{
    if (id.x < numParticles) {
        Particle pi = sorted[id.x];
        float density = 0;

        BEGIN_FOREACH_PARTICLES
            [branch] if (d < radiusSqr) {
                density += poly6(d);
            }
        END_FOREACH_PARTICLES

        density *= mass;

        sorted[id.x].pos.w = density;
        sorted[id.x].vel.w = max(gasConst * (density - restDensity), 0);
    }
}

[numthreads(THREAD_NUM,1,1)]
void CalcForces (uint3 id : SV_DispatchThreadID)
{
    if (id.x < numParticles) {
        Particle pi = sorted[id.x];

        float3 force = 0;

        uint cnt = 0;

        BEGIN_FOREACH_PARTICLES
            [branch] if (d < radiusSqr) {
                float l = sqrt(d);
                if (d > 0) {
                    // Pressure
                    force += (pi.vel.w + pj.vel.w) / (2*pj.pos.w * l) * spiky(l) * diff;
                    // Viscosity
                    force += visco(l) / pj.pos.w * (pj.vel - pi.vel);
                }
                cnt++;
            }

        END_FOREACH_PARTICLES

        // Gravity
        force += gravity * float3(0,-1,0);

        force *= mass;

        forces[id.x].xyz = force;
    }
}


[numthreads(THREAD_NUM,1,1)]
void Step (uint3 id : SV_DispatchThreadID)
{
    if (id.x < numParticles) {
        Particle pi = sorted[id.x];

        float3 currAcc = forces[id.x].xyz / mass;

        [unroll]
        for (uint i = 0; i < 7; i++) {
            if (dot(planes[i], 1) != 0) checkPlane(planes[i], pi.pos.xyz, pi.vel.xyz);
        }

        pi.vel.xyz += currAcc * deltaTime;

        pi.pos.xyz += pi.vel.xyz * deltaTime;
        particles[id.x] = pi;
    }
}